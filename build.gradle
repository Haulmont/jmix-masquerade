/*
 * Copyright (c) 2008-2017 Haulmont.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
}

group 'io.jmix.masquerade'

if (rootProject.hasProperty('buildVersion')) {
    version rootProject.buildVersion
} else {
    version '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

artifacts {
    archives sourceJar
}

if (rootProject.hasProperty('uploadUrl')) {
    configurations {
        deployerJars
    }

    dependencies {
        deployerJars('org.apache.maven.wagon:wagon-http:2.12')
    }

    def uploadUrl = rootProject.uploadUrl
    def uploadUser = rootProject.uploadUser
    def uploadPassword = rootProject.uploadPassword

    uploadArchives.configure {
        repositories.mavenDeployer {
            name = 'httpDeployer'
            configuration = configurations.deployerJars
            repository(url: uploadUrl) {
                authentication(userName: uploadUser, password: uploadPassword)
            }
        }
    }
}

build.dependsOn javadoc

dependencies {
    compile 'com.leacox.motif:motif:0.1'
    compile 'com.codeborne:selenide:5.14.0'
    compile 'org.seleniumhq.selenium:selenium-java:3.141.59'

    testCompile 'org.codehaus.groovy:groovy:3.0.5'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testCompile 'org.slf4j:slf4j-simple:1.7.30'
}