allprojects {
    group 'com.haulmont.masquerade'
    version '0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

configure([project(':masquerade-web'), project(':masquerade-connector')]) {
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = 1.8

    task sourceJar(type: Jar) {
        from sourceSets.main.java
        from sourceSets.main.resources
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }

    if (rootProject.hasProperty('uploadUrl')) {
        configurations {
            deployerJars
        }

        dependencies {
            deployerJars('org.apache.maven.wagon:wagon-http:2.12')
        }

        def uploadUrl = rootProject.uploadUrl
        def uploadUser = rootProject.uploadUser
        def uploadPassword = rootProject.uploadPassword

        uploadArchives.configure {
            repositories.mavenDeployer {
                name = 'httpDeployer'
                configuration = configurations.deployerJars
                repository(url: uploadUrl) {
                    authentication(userName: uploadUser, password: uploadPassword)
                }
            }
        }
    }
}

configure(project(':masquerade-connector')) {
    dependencies {
        compile 'com.squareup.retrofit2:retrofit:2.3.0'
        compile 'com.squareup.retrofit2:converter-gson:2.3.0'
        compile 'com.google.guava:guava:23.0'

        testCompile 'junit:junit:4.12'
    }
}

configure(project(':masquerade-web')) {
    apply plugin: 'groovy'

    dependencies {
        compile 'com.codeborne:selenide:4.8'
        compile 'org.seleniumhq.selenium:selenium-java:3.7.1'

        testCompile 'org.codehaus.groovy:groovy-all:2.4.12'
        testCompile 'junit:junit:4.12'
    }
}